|%
+$  note  [by=@p as=tape if=[@ux @p] to=(list @p) re=tape text=tape]
+$  history  [regards=tape versions=(list note)]
+$  store  (list history)
++  add-note-history
  |=  [n=note h=history]
  ^-  history
  ?:  =(regards.h re.n)
    ?~  versions.h
      [regards.h ~[n]]
    [regards.h [n versions.h]]
  [re.n ~[n]]
++  add-note-store
  |=  [n=note s0=store]
  =/  found=history  (find-store-regards s0 re.n)
  ?:  =(found [re.n ~])
    [[re.n ~[n]] s0]
  =/  s1=store  ~
  |-
  ^-  store
  ?~  s0
    s1
  =/  h0=history  +2:s0
  ?:  =(re.n regards.h0)
    $(s0 +3:s0, s1 [[regards.h0 [n versions.h0]] s1])
  $(s0 +3:s0, s1 [h0 s1])
++  default-tags
  |=  ~
  ^-  (set tape)
  (sy ~["*" "/"])
++  extract-backlink
  |=  [start=@ud text=tape]
  =/  cursor=@ud  (add 2 start)
  =/  max=@ud  (lent text)
  =/  backlink=tape  ~
  |-
  ^-  tape
  ?:  =(cursor (dec max))
    ~
  =/  t0=@t  (snag cursor text)
  =/  t1=@t  (snag +(cursor) text)
  ?:  =(~[t0 t1] "]]")
    (flop backlink)
  $(cursor +(cursor), backlink [t0 backlink])
++  extract-backlinks-history
  |=  h=history
  (extract-backlinks-note (snag 0 versions.h))
++  extract-backlinks-note
  |=  n=note
  (extract-backlinks-tape (index-backlinks text.n) text.n)
++  extract-backlinks-store
  |=  s=store
  =/  backlinks=(set tape)  ~
  |-
  ^-  (set tape)
  ?~  s
    backlinks
  $(s +3:s, backlinks (~(uni in backlinks) (extract-backlinks-history +2:s)))
++  extract-backlinks-tape
  |=  [starts=(list @ud) text=tape]
  =/  backlinks=(set tape)  ~
  |-
  ^-  (set tape)
  ?~  starts
    backlinks
  $(starts +3:starts, backlinks (~(put in backlinks) (extract-backlink +2:starts text)))
++  extract-tag
  |=  [oi=@ud text=tape]
  =/  max=@  (lent text)
  =/  allowed=tape  "#-0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz~"
  =/  tag=tape  ~
  |-
  ^-  tape
  ?:  =(oi max)
    (flop tag)
  =/  t=@t  (snag oi text)
  ?~  (find ~[t] allowed)
    (flop tag)
  $(oi +(oi), tag [t tag])
++  extract-tags-history
  |=  h=history
  (extract-tags-note (snag 0 versions.h))
++  extract-tags-note
  |=  n=note
  (extract-tags-tape (index-tags text.n) text.n)
++  extract-tags-store
  |=  s=store
  =/  tags=(set tape)  ~
  |-
  ^-  (set tape)
  ?~  s
    tags
  $(s +3:s, tags (~(uni in tags) (extract-tags-history +2:s)))
++  extract-tags-tape
  |=  [starts=(list @ud) text=tape]
  =/  tags=(set tape)  ~
  |-
  ^-  (set tape)
  ?~  starts
    tags
  $(starts +3:starts, tags (~(put in tags) (extract-tag +2:starts text)))
++  find-store-regards
  |=  [s=store regards=tape]
  |-
  ^-  history
  ?~  s
    [regards ~]
  =/  h=history  +2:s
  ?:  =(regards.h regards)
    h
  $(s +3:s)
++  filter-store-tag
  |=  [unfiltered=store tag=tape]
  =/  filtered=store  ~
  |-
  ^-  store
  ?~  unfiltered
    filtered
  ?:  (~(has in (extract-tags-history +2:unfiltered)) tag)
    $(unfiltered +3:unfiltered, filtered [+2:unfiltered filtered])
  $(unfiltered +3:unfiltered)
++  index-backlinks
  |=  text=tape
  (fand "[[" text)
++  index-tags
  |=  text=tape
  (weld (fand "#" text) (fand "~" text))
--
:: DO NOT EDIT THIS FILE.  Auto-generated from default tanote.md by lit.
