|%
+$  note  [by=@p as=tape to=(list @p) re=tape text=tape]
+$  history  [regards=tape versions=(list note)]
++  default-tags
  |=  ~
  ^-  (set (list @t))
  (sy ~["*" "/"])
++  extract-backlink
  |=  [start=@ud text=(list @t)]
  =/  cursor=@ud  (add 2 start)
  =/  max=@ud  (lent text)
  =/  backlink=(list @t)  ~
  |-
  ^-  (list @t)
  ?:  =(cursor (dec max))
    ~
  =/  t0=@t  (snag cursor text)
  =/  t1=@t  (snag +(cursor) text)
  ?:  =(~[t0 t1] "]]")
    (flop backlink)
  $(cursor +(cursor), backlink [t0 backlink])
++  extract-backlinks-note
  |=  n=note
  (extract-backlinks-tape (index-backlinks text.n) text.n)
++  extract-backlinks-tape
  |=  [starts=(list @ud) text=(list @t)]
  =/  backlinks=(set (list @t))  ~
  |-
  ^-  (set (list @t))
  ?:  =(~ starts)
    backlinks
  $(starts +3:starts, backlinks (~(put in backlinks) (extract-backlink +2:starts text)))
++  extract-tag
  |=  [oi=@ud text=(list @t)]
  =/  max=@  (lent text)
  =/  allowed=(list @t)  "#-0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz~"
  =/  tag=(list @t)  ~
  |-
  ^-  (list @t)
  ?:  =(oi max)
    (flop tag)
  =/  t=@t  (snag oi text)
  ?:  =(~ (find ~[t] allowed))
    (flop tag)
  $(oi +(oi), tag [t tag])
++  extract-tags-note
  |=  n=note
  (extract-tags-tape (index-tags text.n) text.n)
++  extract-tags-tape
  |=  [starts=(list @ud) text=(list @t)]
  =/  tags=(set (list @t))  ~
  |-
  ^-  (set (list @t))
  ?:  =(~ starts)
    tags
  $(starts +3:starts, tags (~(put in tags) (extract-tag +2:starts text)))
++  index-backlinks
  |=  text=(list @t)
  (fand "[[" text)
++  index-tags
  |=  text=(list @t)
  (weld (fand "#" text) (fand "~" text))
--
:: DO NOT EDIT THIS FILE.  Auto-generated from default tanote.md by lit.
